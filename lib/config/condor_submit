#!/usr/bin/perl -w

use strict;
use File::Basename;
use File::Spec;
use Getopt::Long;

sub usage() {
	print STDERR "Usage: $0 -e job_stderr -o job_stdout jobscript\n";
}

sub quote($) {
	my ($file) = @_;
	$file =~ s/'/'\\''/g;
	$file = "'" . $file . "'";
	return $file;
}

# ----------- パラメータ受け取り ------------ #

# 引数 check
if( @ARGV != 5 ) {
    usage();
    exit 1;
}

my $stdout = undef;
my $stderr = undef;
my @others = ();
GetOptions( "o=s" => \$stdout, "e=s" => \$stderr, "<>" => sub { push(@others, @_); } );
if( $Getopt::Long::error || !defined($stdout) || !defined($stderr) || @others != 1 ) {
    usage();
    exit 1;
}

# テンポラリディレクトリパス名
my $tmpdir = $ENV{'XCR_TMP_DIR'};
if(!defined($tmpdir)) {
	$tmpdir = ".";	# 未指定ならばカレント
}

#ジョブスクリプトファイル名
my $jobscript = $others[0];

# Condorコマンドパス名
my $command_path = $ENV{CONDOR_BIN};
# "/opt/condor-7.2.2/bin";
my $condor_submit_command = File::Spec->catfile( $command_path, "condor_submit" );
my $condor_rm_command = File::Spec->catfile( $command_path, "condor_rm" );

# ----------- submit description file 作成 ------------ #

my $Submit_file = File::Spec->catfile($tmpdir, "jd.$$.sub");
open(SUBFILE, ">$Submit_file") || die "$0: cannot open submit description file. (file: $Submit_file)\n";
print SUBFILE "Transfer_Executable = True \n";
print SUBFILE "getenv = True \n";
print SUBFILE "should_transfer_files = YES \n";
print SUBFILE "when_to_transfer_output = ON_EXIT_OR_EVICT \n";
print SUBFILE "universe = vanilla \n";
print SUBFILE "+out = \"$stdout\" \n";
print SUBFILE "+err = \"$stderr\" \n";
print SUBFILE "Executable = $jobscript \n";
# 入力ファイル達
if( exists($ENV{'XCR_INPUTFILE'}) ) {
	print SUBFILE "transfer_input_files = $ENV{'XCR_INPUTFILE'} \n";
}
# 出力ファイル達
if( exists($ENV{'XCR_OUTPUTFILE'}) ) {
	print SUBFILE "transfer_output_files = $ENV{'XCR_OUTPUTFILE'} \n";
}
print SUBFILE "+XCRJobRequestName = \"$ENV{'LOGNAME'}\" \n";
print SUBFILE "Queue \n";
close( SUBFILE );

# -----------ジョブスクリプトに実行パーミッションの付加  ------------ #
chmod 0755, $jobscript;


# ----------- condor_submit コマンド実行 ------------ #

my $cmd = quote( $condor_submit_command );
$cmd .= " " . quote( $Submit_file );
my @condor_submit_out = `$cmd`;
my $stat = $?;
my $count = 0;
my $JobID = undef;
my $jobs = undef;
for(@condor_submit_out) {
	if(/(\d+)\s+job\(s\) submitted to cluster\s+(\d+)\./) {
		$count++;
		if($count > 1) {
			my @rm_out = `$condor_rm_command $JobID`;
		}
		$jobs = $1;
		$JobID = $2;
	}
}
if (($count == 1) && ($jobs == 1)) {
	print "$JobID\n";
	exit 0;
}
elsif ($stat == 0) {
	print STDERR @condor_submit_out;
	exit 1;
}
else {
	print STDERR @condor_submit_out;
	exit $stat;
}
